// Groovy

plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'maven-publish'
}

group = 'eu.lilithmonodia'
version = '0.2'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'commons-io:commons-io:2.11.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

jar {
    manifest {
        attributes('Main-Class': 'eu.lilithmonodia.winestock.Launcher')
    }
}

application {
    mainModule = 'eu.lilithmonodia.winestock'
    mainClass = 'eu.lilithmonodia.winestock.Launcher'
}

javafx {
    version = '17.0.7'
    modules = ['javafx.controls', 'javafx.fxml']
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher { name = 'WineStock' }
}

// Configure JlinkZip task
jlinkZip {
    group = 'distribution' // Create a distribution group
}

shadowJar {
    archiveBaseName.set('WineStock')
    archiveVersion.set("${version}-runnable")
    archiveClassifier.set('')
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'eu.lilithmonodia.winestock.Launcher'
    }
    configurations = [project.configurations.runtimeClasspath]
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/Monodia3007/WineStock")
            credentials {
                username = findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId 'WineStock'
            artifact jar
        }
    }
}