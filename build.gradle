// Plugin definition - these plugins enable necessary functionalities to complete different stages of the build
plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'maven-publish'
    id 'signing'
    id "org.sonarqube" version "4.4.1.3373"
    id 'jacoco'
}

// Basic Metadata - This section contains basic metadata for your project.
group = 'eu.lilithmonodia'
version = '1.0.1'

// Indicate where Gradle should look for dependencies
repositories {
    mavenCentral() // Gradle should use the central Maven repository for dependencies
}

ext {
    junitVersion = '5.9.2'
}

// Source & Target Java Version
sourceCompatibility = '17'
targetCompatibility = '17'

// Repositories - Specify which repositories gradle will use to fetch project dependencies.
repositories {
    mavenCentral()
}

// Dependencies - Declaring project dependencies (both runtime and test scope).
dependencies {
    // Indicate libraries the project needs to compile and run
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    // Indicate libraries needed for testing this project
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

// Jar Config - Configurations for the `Jar` tasks.
jar {
    manifest {
        attributes 'Main-Class': 'eu.lilithmonodia.winestock.Launcher'
        attributes 'Main-Module': 'eu.lilithmonodia.winestock'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Application Configuration - Details of the application's main entry.
application {
    mainModule = 'eu.lilithmonodia.winestock'
    mainClass = 'eu.lilithmonodia.winestock.Launcher'
}

// JavaFX Configuration - JavaFX related configurations.
javafx {
    version = '17.0.7'
    modules = ['javafx.controls', 'javafx.fxml']
}

// Configures the `jlink` task to create a custom Java runtime image
jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules', 'org.apache.logging.log4j', '--add-modules', 'org.postgresql.jdbc']
    forceMerge 'log4j'
    forceMerge 'postgresql'
    launcher {
        name = 'WineStock'
        mainClassName = 'eu.lilithmonodia.winestock.Launcher'
    }
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions = ['--icon', 'src/main/resources/icon.ico']
            installerOptions = ['--win-shortcut', '--win-shortcut-prompt', '--win-menu', '--win-dir-chooser']
        } else if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            imageOptions = ['--icon', 'src/main/resources/icon.icns']
            installerOptions = ['--mac-package-name', 'WineStock', '--mac-package-identifier', 'eu.lilithmonodia.winestock']
        } else {
            imageOptions = ['--icon', 'src/main/resources/eu/lilithmonodia/winestock/icon.png']
            installerOptions = ['--linux-shortcut', '--linux-menu-group', 'WineStock']
        }
    }
}

// Shadow JAR - Config for creating a fat JAR or an uber JAR that contains all dependencies.
shadowJar {
    archiveBaseName.set('WineStock')
    archiveVersion.set("${version}")
    archiveClassifier.set('shadow')
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'eu.lilithmonodia.winestock.Launcher'
        attributes 'Main-Module': 'eu.lilithmonodia.winestock'
    }
    configurations = [project.configurations.runtimeClasspath]
}

// Javadoc & Source Jar - Config for generating Javadoc and source Jar.
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

afterEvaluate {
    signing {
        sign publishing.publications.mavenJava
    }
}

// Artifacts to be produced by the build
artifacts {
    archives javadocJar, sourcesJar, jar, shadowJar
}

// Configure publishing to publish to OSSRH or Sonatype
publishing {
    repositories {
        maven {
            name = "OSSRH" // Setting repository name
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            // Maven repository URL where the artifacts will be published
            credentials {
                // The Maven credentials for publishing are pulled from environment variables
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar // Specifies that the source code should be published
            artifact javadocJar // Specifies that the Javadoc should be published

            // Configuration to generate a Project Object Model (POM) file for publishing
            pom {
                name = 'WineStock'
                packaging 'jar'
                description = 'WineStock is a JavaFX application for managing wine inventory using a PostgreSQL database. It supports viewing and managing wine details.'
                url = 'https://github.com/Monodia3007/WineStock'

                // Source code management related information
                scm {
                    connection = 'scm:git:https://github.com/Monodia3007/WineStock.git'
                    developerConnection = 'scm:git:https://github.com/Monodia3007/WineStock.git'
                    url = 'https://github.com/Monodia3007/WineStock'
                }

                // Licencing details for the project
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }

                // Developer contact details
                developers {
                    developer {
                        id = 'monodia'
                        name = 'Lilith Camplin'
                        email = 'lilith.camplin@gmail.com'
                    }
                }
            }
        }
    }
}

javadoc {
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
    }
}

sonar {
    properties {
        property "sonar.projectKey", "Monodia3007_WineStock"
        property "sonar.organization", "monodia3007"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPaths", "build/jacoco/test.exec"
    }
}

// Testing - Config for running tests in the project.
test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Make jacocoTestReport run always after the test task.
}

jacocoTestReport {
    dependsOn test // Make jacocoTestReport dependent on the successful completion of test task.
    reports {
        xml.required = true  // Enable XML report.
    }
}